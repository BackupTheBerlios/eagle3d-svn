LED 5mm
//*****************************************************************************
//
//        ********** LED Macro **********
//
//  Created by <UNKNOWN>
//  Modified By MC Cason <farmerboy1967(replace with at)gmail.com>.
//
//  Based off of a 5mm LED, but it can work for other sizes.
//
//  For a 5mm LED, Major manufacturer datasheets show:
//    a maximum height of 8.5mm
//    a maximum of 5.9mm (6mm) for the flange, 1mm high
//    a maximum of 0.45mm (0.5mm) for the flat on the flange
//    a maximum of 0.5mm board clearance for epoxy burr (Not Used)
//  Current Assumptions for a 5mm LED are:
//    Radius of LED
//      D_LED/2                 = 5mm/2               = 2.5mm
//    Height of Flange
//      D_LED/5                 = 5mm/5               = 1.0mm 
//    Flat on flange
//      ((D_LED/5)/2)           = (5mm/5)/2           = 0.5mm
//    Radius of Flange
//      (D_LED/2)+((D_LED/5)/2) = (5mm/2)+((5mm/5)/2) = 3.0mm
//    Height of LED, Minus 1/2 of Crown
//      Height_LED-(D_LED/2)    = 8.5mm-(5mm/2)       = 6.0mm
//
// Color:       Color of LED
// Tra:         Tranparency of LED
// Height:      Height of LED above PCB
// L_Pitch:     Lead Pitch of LED
// L_Width:     Lead Width of LED
// D_LED:       Diameter of LED
// Height_LED:  Height of LED
//
//*****************************************************************************
###############################################################################
LED5MM:0:0:0:0:1:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:DIODE_DIS_LED_254P_5MMX85MM(:Diskrete 5MM LED:
5MM_2PIN_BICOLOR:0:0:0:0:1:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:DIODE_DIS_LED_254P_5MMX85MM(:Diskrete 5MM LED:
2-PIN:0:0:0:0:1:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:DIODE_DIS_LED_254P_5MMX85MM(:Diskrete 5MM LED:
LED10MM:0:0:0:0:1:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:DIODE_DIS_LED_254P_10MMX135MM(:Diskrete 10MM LED:
###############################################################################
DIODE_DIS_LED_GRND
(Color,Tra,Height,L_Pitch,L_Width,D_LED,Height_LED)
###############################################################################
DIODE_DIS_LED_254P_5MMX85MM(col,tra,height)
(col,tra,height,2.54,0.5,5.0,8.5)
DIODE_DIS_LED_254P_10MMX135MM(col,tra,height)
(col,tra,height,2.54,0.5,10.0,13.5)
###############################################################################
###############################################################################
union{
  // Long lead Length
  #local L_Lead = -1*((pin_length+pcb_height)-0.2);

  // Basic variables
  //#local B_LED  = 0.5
  #local B_LED    = 0;
  #local R_LED    = D_LED/2;
  #local H_Flange = D_LED/5;
  #local F_Flange = H_Flange/2;
  #local R_Flange = R_LED+F_Flange;
  #local H_LED    = Height_LED-R_LED;
  #local Lead     = L_Width/2;

  // Body of LED
  merge{
    // Main Body
    cylinder{<0,B_LED,0><0,H_LED,0>R_LED}
    // Rounded top
    sphere{<0,H_LED,0>R_LED}
    // Flange on bottom of LED
    difference{
      // Flange
      cylinder{<0,B_LED,0><0,H_Flange,0>R_Flange}
      // Flat on flange
      box{<R_LED,B_LED-0.01,-R_Flange><R_Flange,
        H_Flange+0.01,R_Flange>
      }
    }
    pigment{Color filter Tra}
  }

  // Internal Structure inside LED
  union{
    // Left internal lead segment
    difference{
      box{<-Lead,0,-Lead><2,3,Lead> translate<-1.27,0,0>}
      box{<0,0,-(Lead+0.001)><2,1,Lead+0.001> rotate<0,0,30>
        translate<-0.05,0.5,0>
      }
      box{<-0.5,-0.001,-(Lead+0.001)><1,1.4,Lead+0.001>}
      lathe{linear_spline 3, <0, 0>, <1.2, 1> <0,1> translate<0,2.2,0>}
    }

    // Right internal Lead Segment
    box{<0,1.8,-Lead><Lead,3.3,Lead> translate<1.27,0,0>}
    difference{
      box{<-1.27,0,-Lead><Lead,2,Lead> translate<1.27,0,0>}
      box{<0,0,-(Lead+0.001)><2,1,Lead+0.001> rotate<0,0,30>
        translate<-Lead,1,0>
      }
      box{<-0.5,-0.001,-(Lead+0.001)><0.5,0.5,Lead+0.001>}
      // Rounding of lead end inside LED
      cylinder{<0,0,-(Lead+0.001)><-0,0,Lead+0.001>0.4
        translate<Lead+1.27,0.75,0>
      }
    }

    // Cone (Reflector)
    lathe{linear_spline 4, <0, 0>, <1, 1>, <1.2,1>,<Lead,0>
      translate<0,2.2,0>
    }

    // Colored element inside cone (Emitter)
    box{<-0.4,0,-0.4><0.4,0.3,0.4>
    pigment{Color}
    finish{ ambient (1 * global_ambient_mul)}
      translate<0,2.7,0>
    }
    interior { ior 1.5 }
    texture{col_silver}
    //pigment{Blue}  // For Testing only
  }

  // External Leads
  #if(pin_short=off)
    // Left lead
    box{<-Lead,0.01,-Lead> <Lead,-25.0-Height,Lead> translate<-1.27,0,0>
      texture{col_silver}
    }
    // Right Lead (about 1mm shorter than left lead)
    box{<-Lead,0.01,-Lead> <Lead,-24.0-Height,Lead> translate< 1.27,0,0>
      texture{col_silver}
    }
  #else
    // Left lead
    box{<-Lead,0.01,-Lead> <Lead,L_Lead-Height,Lead>
      translate<-1.27,0,0> texture{col_silver}
    }
    // Right Lead
    box{<-Lead,0.01,-Lead> <Lead,L_Lead-Height,Lead>
      translate< 1.27,0,0> texture{col_silver}
    }
  #end
  //texture{e3d_tex_silver}

  // Adjust LEDs height, based on height value selected in ULP.
  translate<0,height,0>
}
#end
