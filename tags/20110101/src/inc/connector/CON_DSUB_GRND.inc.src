SUB-D
//Makro f√ºr DSUB Andreas Fecht <info@vetter-und-fecht.de>
//modifiziert und erweitert von Duane Clark <dclark@akamail.com>
####################################################################################################
F09H:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F9(:SUB-D 9polig 90 Grad weiblich:SUB-D 9 Pins 90deg female
F15H:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F15(:SUB-D 15polig 90 Grad weiblich:SUB-D 15 Pins 90deg female
F25H:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F25(:SUB-D 25polig 90 Grad weiblich:SUB-D 25 Pins 90deg female
F37H:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F37(:SUB-D 37polig 90 Grad weiblich:SUB-D 37 Pins 90deg female
M09H:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M9(:SUB-D 9polig 90 Grad maennlich:SUB-D 9 Pins 90deg male
M15H:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M15(:SUB-D 15polig 90 Grad maennlich:SUB-D 15 Pins 90deg male
M25H:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M25(:SUB-D 25polig 90 Grad maennlich:SUB-D 25 Pins 90deg male
M37H:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M37(:SUB-D 37polig 90 Grad maennlich:SUB-D 37 Pins 90deg male
F09HP:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F9(:SUB-D 9polig 90 Grad weiblich:SUB-D 9 Pins 90deg female
HDF15H:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F9(:SUB-D 15polig 90 Grad maennlich:SUB-D 15 Pins 90deg female
F15HP:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F15(:SUB-D 15polig 90 Grad weiblich:SUB-D 15 Pins 90deg female
F25HP:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F25(:SUB-D 25polig 90 Grad weiblich:SUB-D 25 Pins 90deg female
F37HP:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_F37(:SUB-D 37polig 90 Grad weiblich:SUB-D 37 Pins 90deg female
M09HP:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M9(:SUB-D 9polig 90 Grad maennlich:SUB-D 9 Pins 90deg male
M15HP:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M15(:SUB-D 15polig 90 Grad maennlich:SUB-D 15 Pins 90deg male
M25HP:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M25(:SUB-D 25polig 90 Grad maennlich:SUB-D 25 Pins 90deg male
M37HP:0:0:0:0:0:0:0:0:0:0:0:0:0:180:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:0:CON_DSUB_M37(:SUB-D 37polig 90 Grad maennlich:SUB-D 37 Pins 90deg male
####################################################################################################
CON_DSUB_GRND
(pin, female)
####################################################################################################
CON_DSUB_F9()
(9, true)
CON_DSUB_F15()
(15, true)
CON_DSUB_F25()
(25, true)
CON_DSUB_F37()
(37, true)
CON_DSUB_M9()
(9, false)
CON_DSUB_M15()
(15, false)
CON_DSUB_M25()
(25, false)
CON_DSUB_M37()
(37, false)
####################################################################################################
####################################################################################################
#macro DSHELL(ps, vc)
  // the dshell offset is (body size - dshell size)/2 + radius
  #local do = 8.33;    // dshell offset
  #local ds = 11.61;   // do + dshell size - ps - (radius*2)
  union
  {
    cylinder{<do,vc+3,0><do,vc+3,-5.8> 1 }
    cylinder{<ds+ps,vc+3,0><ds+ps,vc+3,-5.8> 1 }
    cylinder{<do+1,vc-3,0><do+1,vc-3,-5.8> 1 }
    cylinder{<ds+ps-1,vc-3,0><ds+ps-1,vc-3,-5.8> 1 }
    box{<do,vc+2,-0.5><ds+ps,vc+4,-5.8>}
    box{<do+1,vc-2,-0.5><ds+ps-1,vc-4,-5.8>}
    box{<0,vc+3,-0.5><1.5,vc-3,-5.8> rotate<0,0,-9.62> translate<ds+ps-2.10,0,0> }
    box{<0,vc+3,-0.5><1.5,vc-3,-5.8> rotate<0,0,9.62> translate<do+0.55,0,0> }
  }
#end

union
{
  #local j = (pin-1)/2; // number of pins in bottom row
  #local hs = 2.758;    // horizontal pin spacing
  #local hc = 9.525;    // vertical centerline
  #local vs = 2.845;    // vertical pin spacing
  #local vc = 6.274;    // vertical centerline
  #local po = 9.97;     // offset to first pin
  #local ps = j*hs;     // total pin spacing
  #local bo = 20;       // body offset = body size - ps
  #local so = 3;        // screwlock offset from body sides

  // the D shaped shell
  difference
  {
    object
    {
      DSHELL(ps, vc)
      translate<-0.5,-0.25,0>
      scale<1.04-(0.0015*j),1.04,1>
      texture{col_silver}
    }

    object
    {
      DSHELL(ps, vc)
      scale<1,1,1.1>
      texture{col_silver}
    }
  }

  #if (female)
    // the plastic piece inside the female dshell
    DSHELL(ps, vc)

    // dshell sockets
    difference
    {
      box{<po-1,vc-4,-0.5><po+ps+1,vc+4,-5.8>}
      #local i = 0;
      #while(i<j)
        cylinder{<po+hs*i,vc+vs/2,-2><po+hs*i,vc+vs/2,-10> 0.5 texture{col_gold} }
        cylinder{<po+hs*(i+0.5),vc-vs/2,-2><po+hs*(i+0.5),vc-vs/2,-10> 0.5 texture{col_gold} }
        #local i = i+1;
      #end
      cylinder{<po+hs*i,vc+vs/2,-2><po+hs*i,vc+vs/2,-10> 0.5 texture{col_gold} }
    }
  #else
    // dshell pins
    union
    {
      #local i = 0;
      #while(i<j)
        cylinder{<po+hs*i,vc+vs/2,-0.5><po+hs*i,vc+vs/2,-5.8> 0.5 texture{col_gold} }
        cylinder{<po+hs*(i+0.5),vc-vs/2,-0.5><po+hs*(i+0.5),vc-vs/2,-5.8> 0.5 texture{col_gold} }
        #local i = i+1;
      #end
      cylinder{<po+hs*i,vc+vs/2,-0.5><po+hs*i,vc+vs/2,-5.8> 0.5 texture{col_gold} }
    }
  #end

  // board pins
  union
  {
    #local i = 0;
    #while(i<j)
      cylinder{<po+hs*i,0,hc+vs/2><po+hs*i,-3,hc+vs/2> 0.5 texture{col_gold} }
      cylinder{<po+hs*(i+0.5),0,hc-vs/2><po+hs*(i+0.5),-3,hc-vs/2> 0.5 texture{col_gold} }
      #local i = i+1;
    #end
    cylinder{<po+hs*i,0,hc+vs/2><po+hs*i,-3,hc+vs/2> 0.5 texture{col_gold} }
  }

  // screwlock backplates
  box{<bo+ps-5.5,3,4><bo+ps-0.5,9.5,4.5> texture{col_silver} }
  box{<0.5,3,4><5.5,9.5,4.5> texture{col_silver} }
  // lower plates
  box{<bo+ps-4.5,3,4><bo+ps-1.5,3.25,hc> texture{col_silver} }
  box{<1.5,3,4><4.5,3.25,hc> texture{col_silver} }
  // mounting tabs
  box{<bo+ps-4.5,0,hc-1><bo+ps-3.5,3.25,hc> texture{col_silver} }
  box{<3.5,0,hc-1><4.5,3.25,hc> texture{col_silver} }

  box{<bo+ps-2.5,0,hc-1><bo+ps-1.5,3.25,hc> texture{col_silver} }
  box{<1.5,0,hc-1><2.5,3.25,hc> texture{col_silver} }

  // the main plastic body
  difference
  {
    box{<0,0,0><bo+ps,10.5,12.6>}
    box{<bo-7+ps,3,4><bo+0.1+ps,12.5,12.7>}
    box{<-0.1,3,4><7,12.5,12.7>}
    cylinder{<bo-7+ps,10.5,4><bo-7+ps,10.5,12.7> 1}
    cylinder{<7,10.5,4><7,10.5,12.7> 1}
    cylinder{<bo-3+ps,3.1,hc><bo-3+ps,-0.1,hc> 1.5}
    cylinder{<3,3.1,hc><3,-0.1,hc> 1.5}
  }

  // upper radius on plastic body
  cylinder{<bo-8+ps,9.5,4><bo-8+ps,9.5,12> 1}
  cylinder{<8,9.5,4><8,9.5,12> 1}

  // thin part of plastic body against the front
  box{<2,12.5,0><bo+ps-2,10.5,2>}
  cylinder{<bo-2+ps,10.5,0><bo-2+ps,10.5,2> 2}
  cylinder{<2,10.5,0><2,10.5,2> 2}

  // thin metal plate backing D shell
  box{<0,1.5,0><bo+ps,11,-0.5>  texture{col_silver} }

  box{<1.5,12.5,0><bo+ps-1.5,10.5,-0.5> texture{col_silver} }
  cylinder{<bo+ps-1.5,11,0><bo+ps-1.5,11,-0.5> 1.5 texture{col_silver} }
  cylinder{<1.5,11,0><1.5,11,-0.5> 1.5 texture{col_silver} }

  box{<1.5,0,0><bo+ps-1.5,1.5,-0.5> texture{col_silver} }
  cylinder{<bo+ps-1.5,1.5,0><bo+ps-1.5,1.5,-0.5> 1.5 texture{col_silver} }
  cylinder{<1.5,1.5,0><1.5,1.5,-0.5> 1.5 texture{col_silver} }

  // screwlocks
  difference
  {
    prism { 0, 5, 7, <3,5>, <-3,5>, <-5,0>, <-3,-5>, <3, -5>, <5,0>, <3,5>  texture{col_silver}
    scale<0.5,1,0.5>
    rotate<90,0,0> }

    cylinder{<0,0,-1><0,0,5> 1.5 texture{col_silver}  }
    translate<so,vc,-5.5>
  }


  difference
  {
    prism { 0, 5, 7, <3,5>, <-3,5>, <-5,0>, <-3,-5>, <3, -5>, <5,0>, <3,5>  texture{col_silver}
    scale<0.5,1,0.5>
    rotate<90,0,0> }

    cylinder{<0,0,-1><0,0,5> 1.5 texture{col_silver}  }
    translate<bo+ps-so,vc,-5.5>
  }

  // internal threads of the screwlocks
  #local i = 0;
    #while(i<12)
      torus { 1.5, 0.25  rotate <90,10,0>  texture{col_silver}
        translate<so,vc, 0.5-i/2> }
      torus { 1.5, 0.25  rotate <90,10,0>  texture{col_silver}
        translate<bo+ps-so,vc,0.5-i/2> }
    #local i = i+1;
  #end

  // screwlock mounting threads (in back)
  #local i = 5;
    #while(i<12)
      torus { 1.2, 0.25  rotate <90,10,0>  texture{col_silver}
        translate<so,vc, 10-i/2> }
      torus { 1.2, 0.25  rotate <90,10,0>  texture{col_silver}
        translate<bo+ps-so,vc,10-i/2> }
    #local i = i+1;
  #end


  cylinder{<0,0,-1><0,0,2.4> 1.2 texture{col_silver} translate<so,vc,5.5> }
  cylinder{<0,0,-1><0,0,2.4> 1.2 texture{col_silver} translate<bo+ps-so,vc,5.5> }

  translate<0-po-ps/2,0,0-hc>
  rotate<0,180,0>
  pigment{Gray30}
}
#end